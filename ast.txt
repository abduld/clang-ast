FIXME: this should match ASTWriter.
        
DeclGroup = Decl*
(FIXME: need to mention DeclGroupRef?)

TranslationUnitDecl = TranslationUnitDecl(Decl*)

DeclContext = BlockDecl
            | FunctionDecl
            | LinkageSpecDecl
            | NamespaceDecl
            | ObjCMethodDecl
            | ObjCContainerDecl
            | TagDecl
            | TranslationUnitDecl

Decl = TranslationUnitDecl
     | NamedDecl
     | LinkageSpecDecl
     | ObjCPropertyImplDecl
     | FileScopeAsmDecl
     | AccessSpecDecl
     | FriendDecl
     | FriendTemplateDecl
     | StaticAssertDecl
     | BlockDecl
     | ClassScopeFunctionSpecializationDecl
     | ImportDecl

NamedDecl = NamespaceDecl
          | UsingDirectiveDecl
          | NamespaceAliasDecl
          | LabelDecl(Identifier)
          | TypeDecl
          | ValueDecl(FIXME: Identifier, Type)
          | TemplateDecl
          | UsingDecl
          | UsingShadowDecl
          | ObjCMethodDecl
          | ObjCContainerDecl
          | ObjCPropertyDecl
          | ObjCCompatibleAliasDecl

TypeDecl = TypedefNameDecl
         | UnresolvedUsingTypenameDecl
         | TagDecl
         | TemplateTypeParmDecl

TypedefNameDecl = TypedefDecl
                | TypeAliasDecl

TagDecl = EnumDecl
        | RecordDecl

RecordDecl = RecordDecl
           | CXXRecordDecl

CXXRecordDecl = CXXRecordDecl
              | ClassTemplateSpecializationDecl

ClassTemplateSpecializationDecl = ClassTemplateSpecializationDecl
                                | ClassTemplatePartialSpecializationDecl

ValueDecl = EnumConstantDecl
          | UnresolvedUsingValueDecl
          | IndirectFieldDecl
          | DeclaratorDecl

DeclaratorDecl = FunctionDecl
               | FieldDecl
               | VarDecl
               | NonTypeTemplateParmDecl

FunctionDecl = FunctionDecl(DeclarationName, NestedNameSpecifier?, FunctionType, Stmt?)
             | CXXMethodDecl

CXXMethodDecl = CXXMethodDecl(DeclarationName, NestedNameSpecifier?, FunctionType, Stmt?)
              | CXXConstructorDecl(DeclarationName, NestedNameSpecifier?, FunctionType, CXXCtorInitializer*, Stmt?)
              | CXXDestructorDecl(DeclarationName, NestedNameSpecifier?, FunctionType, Stmt?)
              | CXXConversionDecl(DeclarationName, NestedNameSpecifier?, FunctionType, Stmt?)

CXXCtorInitializer = CXXCtorInitializer(Type, Expr)
                   | CXXCtorInitializer(Identifier, Expr)

FieldDecl = FieldDecl
          | ObjCIvarDecl
          | ObjCAtDefsFieldDecl

VarDecl = VarDecl(Identifier, NestedNameSpecifier?, Type, Expr?)
        | ImplicitParamDecl
        | ParmVarDecl

TemplateDecl = RedeclarableTemplateDecl
             | TemplateTemplateParmDecl

RedeclarableTemplateDecl = FunctionTemplateDecl
                         | ClassTemplateDecl
                         | TypeAliasTemplateDecl

ObjCContainerDecl = ObjCCategory
                  | ObjCProtocol
                  | ObjCInterface
                  | ObjCImpl

ObjCImpl = ObjCCategoryImpl
         | ObjCImplementation

Stmt = Expr
     | NullStmt()
     | CompoundStmt(Stmt*)
     | LabelStmt(Identifier label, Stmt)
     | AttributedStmt(Attr*, Stmt)
     | IfStmt(VarDecl? var, Expr cond, Stmt then, Stmt? else)
     | SwitchStmt(VarDecl? var, Expr cond, SwitchCase* switchcaselist)
     | WhileStmt(VarDecl? var, Expr cond, Stmt body)
     | DoStmt(Stmt body, Expr cond)
     | ForStmt(Stmt? init, VarDecl? condvar, Expr? cond, Expr? inc, Stmt body)
     | GotoStmt(Identifier label)
     | IndirectGotoStmt(Expr)
     | ContinueStmt()
     | BreakStmt()
     | ReturnStmt()
     | ReturnStmt(Expr)
     | DeclStmt(Decl*)
     | SwitchCase // FIXME: can only appear in SwitchStmt?

     // GNU Extensions
     | AsmStmt

     // MS Extensions
     | MSAsmStmt

     // Obj-C statements
     | ObjCAtTryStmt
     | ObjCAtCatchStmt
     | ObjCAtFinallyStmt
     | ObjCAtThrowStmt
     | ObjCAtSynchronizedStmt
     | ObjCForCollectionStmt
     | ObjCAutoreleasePoolStmt

     // C++ statments
     | CXXCatchStmt
     | CXXTryStmt
     | CXXForRangeStmt

     // Microsoft Extensions.
     | SEHTryStmt
     | SEHExceptStmt
     | SEHFinallyStmt
     | MSDependentExistsStmt

SwitchCase = CaseStmt(Expr lhs, Expr rhs)
           | DefaultStmt(Stmt)

Expr = PredefinedExpr()
     | DeclRefExpr(NestedNameSpecifier?, DeclarationName, TemplateArgument*)
     | IntegerLiteral()
     | FloatingLiteral()
     | ImaginaryLiteral(Expr)
     | StringLiteral()
     | CharacterLiteral()
     | ParenExpr(Expr)
     | UnaryOperator(Expr)
     | OffsetOfExpr(Type, Expr)
     | UnaryExprOrTypeTraitExpr(Type)
     | UnaryExprOrTypeTraitExpr(Expr)
     | ArraySubscriptExpr
     | CallExpr
     | MemberExpr
     | CastExpr
     | BinaryOperator
     | AbstractConditionalOperator
     | CompoundLiteralExpr
     | ExtVectorElementExpr
     | InitListExpr
     | DesignatedInitExpr
     | ImplicitValueInitExpr
     | ParenListExpr
     | VAArgExpr
     | GenericSelectionExpr
     | PseudoObjectExpr
     | AtomicExpr

     // GNU Extensions.
     | AddrLabelExpr
     | StmtExpr
     | ChooseExpr
     | GNUNullExpr

     // C++ Expressions.
     | CXXTypeidExpr
     | CXXBoolLiteralExpr()
     | CXXNullPtrLiteralExpr
     | CXXThisExpr
     | CXXThrowExpr
     | CXXDefaultArgExpr
     | CXXScalarValueInitExpr
     | CXXNewExpr
     | CXXDeleteExpr
     | CXXPseudoDestructorExpr
     | TypeTraitExpr
     | UnaryTypeTraitExpr
     | BinaryTypeTraitExpr
     | ArrayTypeTraitExpr
     | ExpressionTraitExpr
     | DependentScopeDeclRefExpr
     | CXXConstructExpr
     | CXXBindTemporaryExpr
     | ExprWithCleanups
     | CXXUnresolvedConstructExpr
     | CXXDependentScopeMemberExpr
     | OverloadExpr
     | CXXNoexceptExpr
     | PackExpansionExpr
     | SizeOfPackExpr
     | SubstNonTypeTemplateParmExpr
     | SubstNonTypeTemplateParmPackExpr
     | MaterializeTemporaryExpr
     | LambdaExpr

     // Obj-C Expressions.
     | ObjCStringLiteral
     | ObjCBoxedExpr
     | ObjCArrayLiteral
     | ObjCDictionaryLiteral
     | ObjCEncodeExpr
     | ObjCMessageExpr
     | ObjCSelectorExpr
     | ObjCProtocolExpr
     | ObjCIvarRefExpr
     | ObjCPropertyRefExpr
     | ObjCIsaExpr
     | ObjCIndirectCopyRestoreExpr
     | ObjCBoolLiteralExpr
     | ObjCSubscriptRefExpr

     // Clang Extensions.
     | ShuffleVectorExpr
     | BlockExpr
     | OpaqueValueExpr

     // Microsoft Extensions.
     | CXXUuidofExpr

     // OpenCL Extensions.
     | AsTypeExpr

BinaryOperator = BinaryOperator
               | CompoundAssignOperator

AbstractConditionalOperator = ConditionalOperator
                            | BinaryConditionalOperator

CallExpr = CallExpr
         | CXXOperatorCallExpr
         | CXXMemberCallExpr
         | UserDefinedLiteral
         | CUDAKernelCallExpr

CastExpr = ImplicitCastExpr
         | ExplicitCastExpr
         | ObjCBridgedCastExpr

ExplicitCastExpr = CStyleCastExpr
                 | CXXNamedCastExpr
                 | CXXFunctionalCastExpr

CXXNamedCastExpr = CXXStaticCastExpr
                 | CXXDynamicCastExpr
                 | CXXReinterpretCastExpr
                 | CXXConstCastExpr

CXXConstructExpr = CXXConstructExpr
                 | CXXTemporaryObjectExpr

OverloadExpr = UnresolvedLookupExpr
             | UnresolvedMemberExpr

Type = BuiltinType()
     | FunctionType
     | RecordType(Identifier)
     | ElaboratedType(Type)
     | lots

FunctionType = FunctionNoProtoType(Type result)
             | FunctionProtoType(Type result, ParmVarDecl* arg)

TypeLoc = ?

Attr = lots

Notes:
- The structure defined by this grammar is a superset of legal ASTs.
- Nodes can have additional properties beyond those listed. Only
the properties that occur elsewhere in the grammar are listed.
- Many nodes have one or more SourceLocation.
- Decls have a DeclContext and Attr*.
- LabelStmt and GotoStmt actually reference a LabelDecl, not an Identifier.
In the case of a GNU local label, this LabelDecl occurs elsewhere in the tree.
- CXXCtorInitializer actually references a FieldDecl, not and Indentifier.
